plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.1.4'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    maven {
        name = "BMCLAPI"
        url = "https://bmclapi2.bangbang93.com/maven"
    }
    maven {
        url = "https://neoforged.forgecdn.net/releases"
    }
    maven {
        url = "https://neoforged.forgecdn.net/mojang-meta"
    }
    maven {
        url = "https://packages.jetbrains.team/maven/p/skija/maven"
    }
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    maven { url 'https://jitpack.io' }
    maven {
        url = uri('https://mcef-download.cinemamod.com/repositories/releases')
    }
    mavenCentral()
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        argument '--nogui'
    }

    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    clientData {
        arguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

configurations.configureEach {
    resolutionStrategy {
        force 'org.jetbrains:annotations:26.0.1'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    modjij
    implementation.extendsFrom modjij
    compileOnly.extendsFrom(modjij)
    runtimeOnly.extendsFrom(modjij)
    jarJar.extendsFrom modjij
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    modjij 'io.github.humbleui:types:0.2.0'
    modjij 'io.github.humbleui:skija-windows-x64:0.116.4'
    modjij 'org.lwjgl:lwjgl-nfd:3.3.3'
    modjij 'org.lwjgl:lwjgl-nfd::natives-windows'
    modjij 'com.mpatric:mp3agic:0.9.1'
    modjij 'javazoom:jlayer:1.0.1'
    modjij 'com.googlecode.soundlibs:mp3spi:1.9.5.4'
    modjij 'org:jaudiotagger:2.0.3'
    modjij 'com.googlecode.soundlibs:jlayer:1.0.1.4'
    // modjij 'com.github.CCBlueX:mcef:3.1.0-1.21.4'
    modjij 'com.kohlschutter.junixsocket:junixsocket-common:2.10.1'
    modjij "net.lenni0451:MCPing:1.4.2"
    modjij "net.lenni0451:Reflect:1.4.0"
    modjij 'org.jetbrains:annotations:26.0.1'
}

runs {
    configureEach {
        dependencies {
            runtime project.configurations.modjij
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}